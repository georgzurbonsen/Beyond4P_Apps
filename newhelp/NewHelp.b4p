
runtime settings[verbose]	= quiet;

define procedure ( helpx )
{
    local
    {
	help directory[] 	= "." + system info[directory separator];
	help shortcuts file[]	= "Shortcuts.txt";

	table load			( __ help shortcuts __, help directory[] + help shortcuts file[] );
	table delete selected rows	( __ help shortcuts __, [0]='','//' );
	table configure			( __ help shortcuts __, read numerals, no );

	help keys[]		= join([__ help shortcuts __:Key,..]) + escape +Q;
	help keys[]		+= '&arrow-left;&arrow-right;&arrow-up;&arrow-down;&pgup;&pgdn;&home;';

	choice[]		= '0'; // opens the landing page

	clear(alt);

	offset[]		= 2;  // Scrolling offset below 2 title rows


	do
	{
	    cls;
	    help file name[]		= help directory[] + [__ help shortcuts __: Key, choice[], File Name];
	    current row number[]	= [__ help shortcuts __: Key, choice[]];

	    table load				( help page, help file name[], TEXT );

	    table delete selected rows		( help page, ([0]='//*') );

	    table process all selected rows	( help page, ( ((row()-offset[]>=0) | (row()<=1)) & (row()<console(height)+offset[]-3) ), echo( decode entities( [0] )) );

	    cursor(1,console(height));	
	    next choice[] 	= input quick ignore case( 'navigation: page&mango;#&default; &mango;&larr; &rarr; &uarr; &darr;&default;| &mango;q&default;uit ', help keys[] );
	    next row number[]	= [__ help shortcuts __: Key, next choice[]];

	    if (next row number[]>0)
	    {
		choice[]	= next choice[];
		continue loop;
	    }
	    next row number[] = current row number[];
	    check( next choice[] )
	    {
		case( '&arrow-left;' )
		{
		    if (next row number[] > 1) next row number[]--;
		}
		case( '&arrow-right;' )
		{
		    if (next row number[] < table length( __ help shortcuts __ )-1) next row number[]++;
		}
		case( '&arrow-right;', '&arrow-left;' )
		{
		    choice[] = [__ help shortcuts __: Key, next row number[] ];
		    offset[] = 2; // Fixed 2 title rows 
		}
		case( '&home;' )
		{
		    offset[]=2;
		}
		case( '&arrow-down;' )
		{
		    if (offset[] < table length ( help page )-2) offset[]++;
		}
		case( '&arrow-up;' )
		{
		    if (offset[] > 2) offset[]--;
		}
		case( '&pgdn;' )
		{
		    for (i[]=1, i[]<console(height)-3, i[]++) if (offset[] < table length ( help page )-2) offset[]++;
		}
		case( '&pgup;' )
		{
		    for (i[]=1, i[]<console(height)-3, i[]++) if (offset[] > 2) offset[]--;
		}

	    }

//	echo( "Next row number is ",next row number[] ); pause;
//	echo( "Choice is ",choice[] ); pause;

	} while ( next choice[] <> escape,Q );

	clear(back);
	table delete( __ help shortcuts __ );
    }
}

helpx;
echo(done);
interactive;
