define procedure ( table load fixed column file, 
	{ { table, literal }, { file name, literal }, { headers, parameters }, { positions, parameters } } )
{
	// Mit weniger als 10 Befehlen eine Tabelle mit fixen Spaltenbreiten richtig eingelesen.

	table load(table[], file name[], new line);

	// Character positions begin at 0.
	// The excess position 0 will be subtracted by 1 to -1 and this will
	// automatically refer to the last character in the 'middle until' function
	// call you see below.

	positions[] += {0}; // Append an element with zero to the set of positions.


	table insert rows( table[], 0 );
	[table[]:0,0] = Original;
	table insert columns( table[], headers[], "", 0 );
	

	// Achtung: 2 Schleifen: Äussere durch die headers[]-Elemente, dann die Tabellenzeilen bearbeiten

	for all parameters ( headers[], header[], index[] ) 
		table process( table[], [index[]] = trim([Original]{ positions[]{index[]} .. positions[]{index[]+1}-1 }) );
	
	table delete columns( table[], Original );
}



// 'Main' part:  Die oben definierte Prozedur mit dem Namen 'table load fixed column file' 
// kannst Du dann beliebig oft aufrufen.

{	
	table load fixed column file( f, working directory[]+ Fixed Column Table.txt,
		{ Animal Name, Leg count, Estimated size, Class }, { 0, 10, 20, 33 } );

	table save( f, working directory[]+ Fixed Column Table Out.csv);
}
