include ( Office Library );

{
	table load excel file		( table, vagithub.xlsx );

	table delete blank rows		( table );
	table delete rows		( table, 1, 2 );
	table insert columns		( table, type, "", 0 );

	index[] = 1;	

	table process selected rows	( table, ([URL]{0}=' ',chr(160)),  // chr(160) is non-break space (non-ANSI)
						[type] = Details ; index[] = 1;
						[URL] =  replace all([URL], chr(160)+chr(160), chr(160) ) - chr(160);	// Remove all duplicate NBSP symbols and beginning NBSP
						[language..] = parameter set( replace all( '"' + replace all ( [URL], chr(160), '","' ) + '"', chr(160), ' ') );
						[URL] = '';	
					  ,
						[type] = select if( index[]++ == 1, URL, Description );
					) ;

	// Check for all numbers and remove thousand separators (commas)

	table process			( table, for all current table columns ( dummy[], col[] )  if ( (numeral([col[]])>0) & (find([col[]],',')>0) ) [col[]] = clean numeral( [col[]] )	 );


	table process selected rows	( table, [type]==Description, ['description (optional)'] = [URL]; [URL] = '' );	


	// Some rows contain two entries for languages, e.g.  "JavaScript CC0-1.0"

	table process selected rows	( table, ([type]==Details) & (is numeric([members])==false), [language] = [language] + ' ' + [members] );
	table delete cells selected rows( table, ([type]==Details) & (is numeric([members])==false), members );


	// Some rows contain no language and follow with numeric info immediately.  Move the payload to the right.

	table process selected rows	( table,  ([type]==Details) & (is numeric([language])), [members..] = [language..]; [language]='' );


	// Some rows indicate 'need help'

	table process selected rows	( table, (['pull-requests']=+'*need*help*'), [issues] = literal([issues]) + ' ' + ['pull-requests'] );
	table delete cells selected rows( table, (['pull-requests']=+'*need*help*'), 'pull-requests' );


	table fill vertically		( table, URL );

	[table:'>updated last',0] = Date or time elapsed;
	table consolidate		( table, URL, {type,   'description (optional)', language, members, stargazers, issues, 'pull-requests',updated last, Date or time elapsed },
						      {delete, append,                   overwrite if blank }, ' ' );

	
	table save ( table, Result.csv );
}