#!/usr/local/bin/Beyond4P

runtime settings [verbose]=quiet;
include ( Office Library );
include ( Support Library );

{
	runtime settings[verbose] 	= quiet; // Suppresses bla-bla while loading excel file.
	table load excel file		( table, vagithub2.xlsx );
	table load excel file		( lookup patterns, Patterns.xlsx );

	table insert rows		( table, 0 ); // Provide space for headers and incldue them
	[table:..,0] 			= { Raw, type, URL, description, language, license, members, stargazers, issues, 'pull-requests', last updated, need help, unknown };
	table delete blank rows		( table );

	index[] = 1; // Counts the rows between the rows containing details

	table process selected rows	( table, ([Raw]{0}=' ',chr(160)),

		// Following code is executed if the current row begins wtih a space (or non-break space / chr(160) which is the row with details
		[type] 					= Details ; 
		index[] 				= 1;

		// Create an auxiliary table with 2 columns and put the parsed values directly into the first column named 'Values'
		table initialize			( aux table, { Values, Columns } );
		[aux table : Values, ..] 		= tokenize( [Raw], { auto convert, thousand separator, "," }, chr(160) );

		view (aux table);
		view pause;
		
		// Look up the pattern table and record the data type into the column 'Columns' and write the data into the appropriate columns.
		table lookup smart once ignore case  	( aux table, Values, Columns, lookup patterns, Patterns, Fields );
		table process 				( aux table, [^[Columns]] = [Values] );  // Note the '^' symbol. Study chapter 5.13 in the B4P manual

	  ,	// Comma. Following code is executed for all rows not beginning with space (i.e. contains URLS or descriptions) 

		if (index[]++ = 1) 	[URL] 		= [Raw];			// The first row is the URL
		else: 			[description] 	= [Raw];			// And the following rows contain descriptions (if existing)
	) ;


	// Combine the 'need help' info into the issues column

	table process selected rows	( table, [need help]!='', [issues] = literal([issues]) + " " + [need help] );


	// Consolidate the info into one row per item

	table fill vertically		( table, URL );
	table consolidate		( table, URL, {Raw, type, unknown, need help, description, language, license, members, stargazers, issues, 'pull-requests', last updated },
						      {4:delete,                      append,      overwrite if blank }, ' ' );

	table save ( table, Result.csv );
}
