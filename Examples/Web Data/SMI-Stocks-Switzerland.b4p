#!/usr/local/bin/b4p
{

//	In case the web page is not in German, then you need to rename the column headers

	file download overwrite		( "https://www.finanzen.ch/index/liste/smi", smi.html);
	table load			( smi, smi.html, HTML, 'table-responsive' );
	table configure			( smi, read numerals, no ); // Read numbers as literals, not as numerals

//	Most headers contain two items.  Separate them.

	table rename column headers( smi, { "3 Mon. +/- %", "6 Mon. +/- %", "1 Jahr +/- %" }, { "3-Mon. 3-Mon-%", "6-Mon. 6-Mon-%", "1-Jahr 1-Jahr-%" } );

	for all parameters( { Name ISIN, Letzter Vortag, Tief Hoch, "+/- %", "3-Mon. 3-Mon-%", "6-Mon. 6-Mon-%", "1-Jahr 1-Jahr-%" }, h12[] ) // But keep date time column together which is OK.
	{
		h2[] = right last match (h12[],' ');
		h1[] = h12[] - (' ' + h2[]);
		table insert columns		( smi, h2[], '', softquoted literal('>'+h12[]) );
		table rename column headers	( smi, h12[], h1[] );
		table process			( smi, [h2[]] = right last match( [h1[]], ' ' ); [h1[]] -= ' ' +[h2[]] );
	}


	// Contains accent-aigu symbol as thousand separator.  Likely created on a Hewlett Packard windows installation and not Dell which uses sg. quotation marks.
	// Clean numbers up so they can be used on all installations.


	for all parameters( { Letzter, Vortag, Tief, Hoch, "3-Mon.", "6-Mon.", "1-Jahr" }, column[] )
	{
		table process( smi, [column[]] = clean if numeral( [column[]] ) );
	}

	table delete blank columns ( smi );
	table save			( smi, smi.csv );

}
