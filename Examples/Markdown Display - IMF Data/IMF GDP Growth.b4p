#!/usr/local/bin/b4p

runtime settings [verbose]=quiet;
include ( Office Library );
include ( Support Library );

/*
	This B4P example downloads two GDP growth reports (true Excel files with .xlsx),
	reports, align the tables and compare the growth figures.

	Written by Georg zur Bonsen, 21. Jan 2021

*/


{
	watch start;	// Clock the job.

	// Remember: Variables always contain brackets (with or without subscripts) all time.

	gdp growth imf      [] = "GDP Growth IMF.xlsx";

	echo( "This small application shows how to retrieve some economical data and" );
	echo( "and do some analysis with them.", new line );


	echo( "Step 1 - Download the GDP growth data from IMF", new line );

	file download overwrite	( "https://www.imf.org/external/pubs/ft/weo/data/WEOhistorical.xlsx", gdp growth imf[] );

	echo( "Step 2 - Load downloaded excel files" );
	echo( "         Note: The IMF file is a huge one (> 7 MB compressed).  Allow 1-2 minutes ...", new line );

	// This is a procedure implemented in "Office Library.b4p" which loads open format Microsoft Excel files (.xlsx, .xlsm).

	table load excel file	( imf      , gdp growth imf      [], ngdp_rpch );


	echo( "Step 3 - Clean up IMF data", new line );
	echo( "         Cleaning up a big an messy table !!! ... ");

	table keep columns 		( imf, { country, year, -1 } ); // Keep 1st two and the last column, forget all other.
	table rename column headers	( imf, -1, GDP growth );
	table delete selected rows	( imf, [GDP growth] = '.' );

	// The years are still listed vertically.  Move them across columns by doing a simple pivot.

	table process			( imf, [year] = IMF Y + literal([year]); [GDP growth] = round([GDP growth], 0.1 ) ); // Round the numbers for clarity
	table spread 			( imf, GDP growth, [year] );
	table consolidate		( imf, country, { year, GDP growth } + [ imf : '>GDP growth'.., 0 ], { delete, delete, sum } );


	// Remove numbers behind countries (were references to footnotes)

	table process			( imf, 		[country] = ![country] - '1'-'2'-'3'-'4'-'5'-'6'-'7'-'8'-'9'-'0'-',' );


	echo( "Step 6 - Identify the years", new line );

	years [imf]			= [ imf ::'IMF*', 0 ] -^ 'IMF ';  // Here you see how to use member (child) variables
	echo				( "Years in IMF       listing : ", years [imf]       );

	table save			( imf, Result.csv );

	echo (new line, "processing took ", watch stop()/1000, " seconds."  );
	echo ("Everything completed.  Open Result.csv" );



}
