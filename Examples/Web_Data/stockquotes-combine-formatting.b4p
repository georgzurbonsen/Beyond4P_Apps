#!/usr/local/bin/b4p

	include ( Style Library );

//	Step 1: Download two web pages.

	for all parameters ( {nasdaq100, sp500} , listing[] )
	{
		file download overwrite		( "https://www.slickcharts.com/" + listing[], listing[] + .html);
		table load			( listing[] ,listing[] + .html,  HTML, "Components of the" );

		table rename column headers	( listing[], "Weight", "Weight " + listing[] ); // Weight info is specific to SP and Nasdaq, so add the listing name
		table save			( listing[], listing[] + " as loaded.csv" );
	}

//	Step 2:	Combine the two tables

	table merge extend columns		( nasdaq100, sp500, Symbol );
	table rename				( sp500, stocks );

//	Step 3: Cleanup

	table correct headers			( stocks, '*Price*', Price );
	table rearrange columns			( stocks, { '#', Company, Symbol, Price, Chg, '% Chg' } ); 		// Weightings follow afterwards
	
	// Percent value gets converted to regular number, price value is claned up

	table process				( stocks, ['% Chg'] = smart numeral( middle( ['% Chg'], '(', ')' )); [Price] = clean numeral([Price]) );
	table sort rows				( stocks, Company );
	table process				( stocks, ['#'] = row() );

//	Step 4: Add some color

	table style auto width			( stocks );
	table style theme			( stocks, Zebra Vertical Lines, pattern, 2, table, "gridlines, false" ); // Double zebra patterns, no grid lines
	table process				( stocks, // Negative numbers: red, positive numbers: navy blue
	    table style cells			    ( stocks, { 'Chg', '% Chg' }, { 2: row() }, single, text color, select if ( [Chg]>0, navy, red ) ) );

	table style columns			( stocks, '% Chg', sheet, number format, "0.00%" ); // Value to show as percent.
	table style table			( stocks, sheet, freeze rows, 1, autofilter, 0);

//	Step 5: Save the artwork

	table save excel file			( stocks, "NASDAQ and SP500", Stocks.xlsx );
